!  testa divisibilidade em Assembly as88
!
!  (c) rro, 2015-02-25
!
!  para montar o programa:
!    $ ./as88 divis.s readnum.s

_EXIT	   =	  1	! seven system call numbers
_READ	   =	  3
_WRITE	   =	  4
_OPEN	   =	  5
_CLOSE	   =	  6
_CREAT	   =	  8
_LSEEK	   =	 19
_GETCHAR   =	117	! five system subroutine numbers
_SPRINTF   =	121
_PUTCHAR   =	122
_SSCANF	   =	125
_PRINTF	   =	127
STDIN	   =	  0	! three initially opened file descriptors
STDOUT	   =	  1
STDERR	   =	  2

.SECT .TEXT		! Definition of section header labels
TxtSecHd:
.SECT .DATA
DatSecHd:
.SECT .TEXT

.SECT .TEXT

start:
	MOV	BP,SP		! salva SP
	PUSH	pr1		! endereco da string
	PUSH	_PRINTF		! no. da syscall
	SYS			! executa syscall 

	CALL	readnum		! le string e converte para numero
	MOV	(x),AX		! armazena o numero lido em 'x'

	PUSH	pr2		! endereco da string
	PUSH	_PRINTF		! no. da syscall
	SYS			! executa syscall 

	CALL	readnum		! le string e converte para numero
	MOV	(y),AX		! armazena o numero lido em 'y'

	MOV	CX,AX		! divisor em CX (y)
	MOV	DX,0		! dividendo=DX:AX - DX=0, AX=x
	MOV	AX,(x)		! 
        IDIV    CX              ! quociente em AX, resto em DX
        CMP     DX,0		! compara DX com 0
        JZ      divisivel       ! se for igual, e' divisivel

ndivisivel:
	MOV	BX,str_nDIV	! BX=end de "nao eh"
        JMP     imprime
	
divisivel: 
	MOV    	BX,str_div	! BX=end de "eh"
	
imprime:
	PUSH	(y)		! valor de y
	PUSH	BX		! endereco da string
	PUSH	(x)		! valor de x
	PUSH	pr3		! 1o argumento de printf() e' pr3
	PUSH	_PRINTF
	SYS

encerra:    
	MOV	SP,BP		! limpa pilha
	PUSH	0		! codigo de retorno=0
	PUSH	_EXIT		
	SYS			! encerra o programa
fim:
	
.SECT	.DATA
pr1:		.ASCIZ	'Entre o 1o numero: '
pr2:		.ASCIZ	'Entre o 2o numero: '
str_div:	.ASCIZ	'eh'
str_nDIV:	.ASCIZ	'nao eh'
pr3:		.ASCIZ	'\n%d %s divisivel por %d\n'
x:		.WORD	0
y:		.WORD	0

.SECT	.BSS
! subrotina para ler um numero da entrada padrao em as88
! - numero lido e' retornado em AX
! - nao e' verificado se o numero lido cabe em 16 bits
!
! (c) rro, 2015-02-24

_RNBSIZ=8			! tamanho do buffer de leitura

.SECT	.TEXT
	
readnum:
	PUSH	BP		! salva BP
	MOV	BP,SP		! salva SP em BP
	PUSH	_RNBSIZ		! no. de bytes a ler
	PUSH	_rnbuf		! endereco da string
	PUSH	STDIN		! arquivo de entrada
	PUSH	_READ		! no. da syscall
	SYS			! executa syscall 

	PUSH	_rnum		! onde sera' armazenado o numero
	PUSH	_rnssf		! string de formato
	PUSH	_rnbuf		! string lida
	PUSH	_SSCANF
	SYS

	MOV	AX,(_rnum)	! guarda o numero em AX
	MOV	SP,BP		! limpa a pilha
	POP	BP		! restaura BP
	RET			! encerra funcao
	
.SECT	.DATA
_rnbuf:	.SPACE	_RNBSIZ
_rnssf:	.ASCIZ	'%d'
_rnum:	.WORD	0
	
.SECT	.BSS
